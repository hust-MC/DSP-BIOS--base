   .align 2
   .sect "const_data"
c  ;Q15  32bits
 .long          0,-968517221,-115045170, 109395968, 131862528, -58680114,  34630042,  70375219,  41019264,  17425408, -13598041,  31603354, -51919794
 .long  166454886, -54778265, 172929372,-231273217, 144725579, 118130925,  43173406, -86588963,  16912896,  41330092, -50223587, -24452081, -11757517
 .long -147264379, -14662870,  47022214,  79109298,  27093960,  52556725,  37559423,  -1328582, -21317400,  12460856,  11046325, -22786262,   5210963
 .long  -20056186,  17088420, -13579598,  17460225, -26003767, -21050098, -49361013,  25834875,  -9364931, -19034271, -14081355,  18269655,  17018934
 .long   51046549, -28978868,   9993014,  -7383569,   2423226, -12234868,  -2664275,   5017657, -15944881,   9421333,         0,   -784838,  -4786574
 .long   14125074,  45278865, -18968220,  -1417586,   2381634,   -324140,   1113178,   1922086,   4859687, -11923037,   7508407,    519122,  12039733
 .long  -12574176,  26786185,  20762966, -11336582,     -7624,   1109306,  -1899466,    452343,   2069630,   -741123,    541591,  -2158832,  -3412768
 .long  -71479479, -21257326,   4831457,  10278104,   2225574,  -2095059,   -108167,     38177,   -952866,   2172355,   1379228,   1137805,   1534249
 .long   24600576, -38591846,  13029471, -17346629,   7824095,   1732190,  -1059653,     -4106,   -106786,   -567300,   1045697,   1342442,   -575342
 .long  -83912611,  45927155,  34261689, -12377044,  -8942277,   4560763,    715890,   -668906,    119744,   -181611,     -8698,         0,   -251099
 .long   19132795,   1380791,  23829987,  18382195, -15743434,  -1489373,  -2233034,   -214501,   -200081,   -153670,    -44739,     98054,    -30907
 .long    4584765,  16084421,  -8597484, -19620983,   4672102,  -1850427,  -4388675,   -671220,   -375501,   -178280,    -22805,     77920,    -27342
 .long  -11757517,   7816445,  51056802, -41483651,   6567127,   2047662,         0,         0,    251100,   -278174,    -36456,     14884,     -1859  

k   ;Q31  32bits
 .long  0,         0, 715827883, 572662306, 552210081, 545392673, 542293851, 540625254, 539624096, 538976288, 538533051, 538216453, 537982446
 .long  0,         0,         0, 429496730, 490853405, 511305630, 520602096, 525607886, 528611360, 530554784, 531884495, 532834289, 533536310
 .long  0,         0,         0,         0, 306783378, 409044504, 455526834, 480555781, 495573150, 505290270, 511938826, 516687795, 520197902
 .long  0,         0,         0,         0,         0, 238609294, 347068064, 405468941, 440509466, 463182748, 478696045, 489776972, 497967223
 .long  0,         0,         0,         0,         0,         0, 195225786, 300347363, 363420310, 404232216, 432156152, 452101821, 466844271
 .long  0,         0,         0,         0,         0,         0,         0, 165191050, 264305680, 328438676, 372319146, 403662340, 426829048
 .long  0,         0,         0,         0,         0,         0,         0,         0, 143165577, 235802126, 299185028, 344458530, 377921553
 .long  0,         0,         0,         0,         0,         0,         0,         0,         0, 126322568, 212753798, 274490391, 320121786
 .long  0,         0,         0,         0,         0,         0,         0,         0,         0,         0, 113025455, 193757923, 253429747
 .long  0,         0,         0,         0,         0,         0,         0,         0,         0,         0,         0, 102261126, 177845437
 .long  0,         0,         0,         0,         0,         0,         0,         0,         0,         0,         0,         0,  93368854
 .long  0,         0,         0,         0,         0,         0,         0,         0,         0,         0,         0,         0,         0
 .long  0,         0,         0,         0,         0,         0,         0,         0,         0,         0,         0,         0,         0

cd    ;Q9  16bits
 .word      0,  4096,-11596,   512, -5599,-11289, -5173,  2746,  2574,     0,     0,     0,     0
 .word -10700,  5427, -6916, -4075,  7934,  3644,  3871, -1815, 10296,     0,     0,     0,     0
 .word -20573, -6473,  -354, -1189,-14024,-12590, -2294, -4447,-11485,     0,     0,     0,     0
 .word   7838, -6939,  -242, -2630,  6640, -2650, 11732, 11533,  6362,     0,     0,     0,     0
 .word   6233,  3206,  6211,    38, -1438,   114, -5866,  3794, -4106,     0,     0,     0,     0
 .word      0,  6689,  5060,  5452,  -394,  -286,  -714,  1581,  1519,     0,     0,     0,     0
 .word  -5806,-14537, -2039, -1396,  -356,   241,   481,  -495,  1406,     0,     0,     0,     0
 .word  10896,  5931,  2097,  1897, -2528,  -247,    33,   199,  -897,     0,     0,     0,     0
 .word  -6863,  2871,  6362,  5476,   759,  -702,   513,   128,   128,     0,     0,     0,     0
 .word      0,     0,     0,     0,     0,     0,     0,     0,     0,     0,     0,     0,     0
 .word      0,     0,     0,     0,     0,     0,     0,     0,     0,     0,     0,     0,     0
 .word      0,     0,     0,     0,     0,     0,     0,     0,     0,     0,     0,     0,     0
 .word      0,     0,     0,     0,     0,     0,     0,     0,     0,     0,     0,     0,     0

MAXORD   .set 12      		     ;12阶球谐系数
A0       .set 08C4Ah
A1       .set 063A8h 		     ;a=6378.137   in Q18,A0低位,A1高位
B0       .set 025Fh 
B1       .set 06353h 		     ;b=6356.7523142 in Q18,B0低位,B1高位 
A20      .set 0CBA4h
A21      .set 026CBh 		     ;a^2 Q4
B20      .set 04EDAh
B21      .set 02689h 		     ;b^2 Q4  
RE0      .set 0CCCDh 
RE1      .set 0638Ch 		     ;re=6371.2 in Q18,RE0低位,RE1高位   
  
_epoch .usect "xc_data",1       ;Q4 
_mctime  .usect "xc_data",1     ;Q4 
_alt   .usect "xc_data",2       ;Q18 
_rlat  .usect "xc_data",2       ;Normalized Q31
_rlon  .usect "xc_data",2 	     ;Normalized Q31
srlat  .usect "xc_data",2       ;Q31 
crlat  .usect "xc_data",2       ;Q31
srlat2 .usect "xc_data",2       ;Q31
crlat2 .usect "xc_data",2       ;Q31
a      .usect "xc_data",2
b      .usect "xc_data",2
re     .usect "xc_data",2
a2     .usect "xc_data",2
b2     .usect "xc_data",2 
d      .usect "xc_data",2 
r      .usect "xc_data",2

ca     .usect "xc_data",2
sa     .usect "xc_data",2
ct     .usect "xc_data",2
st     .usect "xc_data",2

aor    .usect "xc_data",2
ar     .usect "xc_data",2
par    .usect "xc_data",2 
temp1  .usect "xc_data",2
temp2  .usect "xc_data",2
                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                                      
bx     .usect "xc_data",2
bx0    .usect "xc_data",2
by     .usect "xc_data",2
by0    .usect "xc_data",2
bz     .usect "xc_data",2
bz0    .usect "xc_data",2  
byy    .usect "xc_data",2
byy0   .usect "xc_data",2


;换页
_x     .usect "xc_data",2
_y     .usect "xc_data",2
_z     .usect "xc_data",2
_h     .usect "xc_data",2
_ti    .usect "xc_data",2
h_q13  .usect "xc_data",2  
spp    .usect "xc_data",13*2    ;Q31 
cp     .usect "xc_data",13*2    ;Q31

;换页
pp     .usect "xc_data",169*2
dpp    .usect "xc_data",169*2
z_q13  .usect "xc_data",2
dt     .usect "xc_data",1

                                                          
   .ref _qsqrt
   .ref _qcoslt
   .ref _qsinlt
   .ref _div32
   .global _magnetic_calcu
   .global _epoch 
   .global _mctime 
   .global _alt   
   .global _rlat 
   .global _rlon
   .global _x
   .global _y
   .global _z
   .global _h
   .global _ti
  

;//////////////////////////////////////////////////////////;
;变量使用说明:											   ;
;    epoch 2005年（WMM2005－2010），定标Q4后的起始年份     ;
;    time  定标Q4后的测试年份                              ;
;    alt： 定标Q4后的海拔高度                              ;
;    rlat：规一化Q31后的纬度                               ;
;    rlon：规一化Q31后的经度                               ;
;    x：   地磁场北向分量 Q13                              ;
;    y：   地磁场东向分量 Q13                              ;
;    z：   地磁场垂向分量 Q13                              ;
;//////////////////////////////////////////////////////////;

   .sect "xc_program"
_magnetic_calcu:
   ;保护现场
   PUSH ACC
   PUSH XAR0
   PUSH XAR1
   PUSH XAR2
   PUSH XAR3
   PUSH XAR4
   PUSH XAR5
   PUSH XAR6
   PUSH XAR7
   PUSH XT
   PUSH P
   PUSH ST0
   PUSH ST1

   SETC OBJMODE
   SETC INTM     
   CLRC SXM
   CLRC OVM
   MOVW DP,#_epoch
   MOV AL,@_mctime
   SUB AL,@_epoch                ;dt=time-epoch
   LSL AL,#2
   MOVW DP,#dt
   MOV @dt,AL                    ;dt Q6 16bits
   
   MOVW DP,#_rlon
   MOVL ACC,@_rlon
   LC  _qsinlt                   ;srlon=sin(rlon)
   MOVW DP,#spp
   MOVL @spp+1*2,ACC             ;spp[1]=srlon Q31
   
   MOVW DP,#_rlon
   MOVL ACC,@_rlon
   LC  _qcoslt                   ;crlon=cos(rlon)
   MOVW DP,#cp
   MOVL @cp+1*2,ACC              ;cp[1]=crlon Q31
  
   MOVW DP,#_rlat
   MOVL ACC,@_rlat
   LC  _qsinlt                   ;srlat=sin(rlat) Q31
   MOVL @srlat,ACC
   
   MOVL ACC,@_rlat
   LC  _qcoslt                   ;crlat=cos(rlat)  Q31
   MOVL @crlat,ACC
   
   SPM 0
   MOVL XT,@srlat
   IMPYL P,XT,@srlat
   QMPYL ACC,XT,@srlat           ;srlat2=srlat*srlat,Q31
   LSL64 ACC:P,#1
   MOVL @srlat2,ACC
   
   MOVL XT,@crlat
   IMPYL P,XT,@crlat
   QMPYL ACC,XT,@crlat           ;crlat2=crlat*crlat,Q31
   LSL64 ACC:P,#1
   MOVL @crlat2,ACC
   
   MOVW DP,#spp
   MOV @spp,#0h      
   MOV @spp+1,#0h                ;0.0 in Q31 format
   MOV @cp,#0FFFFh
   MOV @cp+1,#7FFFh              ;1.0 in Q31 format
   MOV AR1,#(MAXORD-2)
   SPM 0
   ;SETC OVM
   MOVL XAR2,#spp+2
   MOVL XAR3,#cp+2
LOOP0:
   MOVL XT,@spp+2
   IMPYL P,XT,*XAR3
   QMPYL ACC,XT,*XAR3
   MOVL XAR4,P
   MOVL XAR5,ACC                 ;XAR5:XAR4=sp[1]*cp[n-1]
   MOVL XT,@cp+2
   IMPYL P,XT,*XAR2
   QMPYL ACC,XT,*XAR2++
   ADDUL P,XAR4
   ADDCL ACC,XAR5
   LSL64 ACC:P,#1
   MOVL *XAR2,ACC                ;spp[n]=spp[1]*cp[n-1]+cp[1]*sp[n-1],Q31
   
   MOVL XT,@spp+2
   IMPYL P,XT,*--XAR2
   QMPYL ACC,XT,*XAR2
   NEG64 ACC:P
   MOVL XAR4,P
   MOVL XAR5,ACC                 ;XAR5:XAR4=-sp[1]*sp[n-1]
   MOVL XT,@cp+2
   IMPYL P,XT,*XAR3
   QMPYL ACC,XT,*XAR3++
   ADDUL P,XAR4
   ADDCL ACC,XAR5
   LSL64 ACC:P,#1 
   MOVL *XAR3,ACC                ;cp[n]=cp[1]*cp[n-1]-sp[1]*sp[n-1],Q31 
   ADDB XAR2,#1*2                ;注意:寻址32位数,间接寻址指针增量加2，而不是1
   BANZ LOOP0,AR1--  

;###########################################################;
;###########################################################;   
;   CONVERT FROM GEODETIC COORDS TO SPHERICAL COORDS        ;
;            大地坐标转化为球心坐标                         ;
;###########################################################;
;###########################################################;
   CLRC OVM
   MOVW DP,#a 
   MOV @a,#A0
   MOV @a+1,#A1                  ;a,b,re in Q18
   MOV @b,#B0
   MOV @b+1,#B1
   MOV @re,#RE0
   MOV @re+1,#RE1   
   MOV @a2,#A20 
   MOV @a2+1,#A21                ;a2 in Q4  
   MOV @b2,#B20
   MOV @b2+1,#B21                ;b2 in Q4
   
   ;////////////////////////////////////////////////////////;   
   ;     d=sqrt[a2*crlat2+b2*srlat2,Q18,32位                ;
   ;////////////////////////////////////////////////////////;
   SPM 0
   CLRC C
   MOVL XT,@a2
   IMPYL P,XT,@crlat2
   QMPYL ACC,XT,@crlat2
   MOVL XAR1,P
   MOVL XAR2,ACC
   MOVL XT,@b2
   IMPYL P,XT,@srlat2
   QMPYL ACC,XT,@srlat2
   ADDUL P,XAR1
   ADDCL ACC,XAR2
   LSL64 ACC:P,#1
   LC _qsqrt  
   MOVL @d,ACC      
                                                                                                                                                                                                                                                                                                                                                                                                                                                                                              
   ;$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$;
   ;r=sqrt[alt*alt+2*alt*d+(a2/d)^2*crlat2+(b2/d)^2*srlat2] ; 
   ;         Q18, 32位                                      ;
   ;$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$;
   SPM 0
   CLRC C 
   MOVL ACC,@a2;
   MOVL P,@d
   LC _div32 
   MOVL XAR0,P 		  	         ;XAR0=a2/d in Q18 
   MOVL XT,@crlat2
   IMPYL P,XT,XAR0
   QMPYL ACC,XT,XAR0
   LSL64 ACC:P,#1
   MOVL XT,ACC 			         ;XT=(a2/d)*crlat2 in Q18
   IMPYL P,XT,XAR0
   QMPYL ACC,XT,XAR0
   MOVL XAR1,ACC
   MOVL XAR0,P  		         ;XAR1:XAR0=(a2/d)^2*crlat2 in Q36 
   
   MOVL ACC,@b2;
   MOVL P,@d
   LC _div32 
   MOVL XAR2,P			         ;XAR2=b2/d in Q18 
   MOVL XT,@srlat2
   IMPYL P,XT,XAR2
   QMPYL ACC,XT,XAR2
   LSL64 ACC:P,#1
   MOVL XT,ACC  		         ;XT=(b2/d)*srlat2 in Q18
   IMPYL P,XT,XAR2
   QMPYL ACC,XT,XAR2
   MOVL XAR3,ACC
   MOVL XAR2,P 		             ;XAR3:XAR2=(b2/d)^2*srlat2 in Q36
   
   MOVL XT,@_alt
   IMPYL P,XT,@d
   QMPYL ACC,XT,@d
   LSL64 ACC:P,#1
   MOVL XAR5,ACC
   MOVL XAR4,P                   ;XAR5:XAR4=2*alt*d in Q36
   
   IMPYL P,XT,@_alt
   QMPYL ACC,XT,@_alt            ;ACC:P=alt*alt
   
   ADDUL P,@XAR0
   ADDCL ACC,@XAR1
   ADDUL P,@XAR2
   ADDCL ACC,@XAR3
   ADDUL P,@XAR4
   ADDCL ACC,@XAR5
   LC _qsqrt
   MOVL @r,ACC                   ;r Q18,32位
   

   MOVL ACC,@_alt
   ADDL ACC,@d
   MOVL P,@r
   LC _div32
   NOP
   NOP
   ASR64 ACC:P,#1
   TBIT PH,#15
   SBF flg,NTC
   MOV PL,#0FFFFh
   MOV PH,#7FFFh
flg:
   SPM 0
   MOVL @ca,P                    ;ca=(alt+d)/r in Q31,防止溢出
   

   ;sa=[(a+b)*crlat/r]*[(a-b)*srlat/d]
   MOVL ACC,@a
   SFR ACC,#1
   MOVL P,@b
   SPM -1
   ADDL ACC,P<<PM                ;a+b in Q17
   MOVL XT,ACC
   QMPYL ACC,XT,@crlat
   MOVL P,@r
   LC _div32
   MOVL XAR0,P                   ;XAR0=(a+b)*crlat/r in Q30
   
   MOVL ACC,@a
   SUBL ACC,@b
   LSL ACC,#2    
   MOVL XT,ACC
   QMPYL ACC,XT,@srlat
   MOVL P,@d
   LC _div32
   MOVL XT,P                     ;XT=(a-b)*srlat/d in Q33
   QMPYL ACC,XT,XAR0
   MOVL @sa,ACC                  ;sa Q31
   
   CLRC C
   SPM 0
   MOVL XT,@srlat
   IMPYL P,XT,@sa
   QMPYL ACC,XT,@sa
   MOVL XAR0,P
   MOVL XAR1,ACC
   MOVL XT,@crlat
   IMPYL P,XT,@ca
   QMPYL ACC,XT,@ca
   ADDUL P,XAR0
   ADDCL ACC,XAR1
   LSL64 ACC:P,#1
   MOVL @ct,ACC                  ;ct=crlat*ca+srlat*sa,Q31
   
   CLRC C
   IMPYL P,XT,@sa
   QMPYL ACC,XT,@sa
   NEG64 ACC:P
   MOVL XAR0,P
   MOVL XAR1,ACC
   MOVL XT,@srlat
   IMPYL P,XT,@ca
   QMPYL ACC,XT,@ca
   ADDUL P,XAR0
   ADDCL ACC,XAR1
   LSL64 ACC:P,#1
   MOVL @st,ACC                  ;st=srlat*ca-crlat*sa,Q31
   
   MOVL ACC,@re
   MOVL P,@r
   LC _div32
   ;SPM -2,错误，因其是符号移位
   LSR64 ACC:P,#2
   MOVL @aor,P                   ;aor=re/r in Q30
   MOVL XT,@aor
   IMPYL P,XT,@aor
   QMPYL ACC,XT,@aor
   LSL64 ACC:P,#2
   MOVL @ar,ACC                  ;ar=aor*aor in Q30
   
   MOVW DP,#bx
   MOVB ACC,#0
   MOVL @bx,ACC
   MOVL @by,ACC
   MOVL @byy,ACC
   MOVL @bz,ACC
   MOVL @bx0,ACC
   MOVL @by0,ACC
   MOVL @byy0,ACC
   MOVL @bz0,ACC
   MOVW DP,#pp
   MOV @pp,#0h
   MOV @pp+1,#4000h              ;pp[0]=1,Q30
   MOVW DP,#dpp
   MOV @dpp,#0
   MOV @dpp+1,#0                 ;dpp[0]=0.Q30
 

;###########################################################;
;###########################################################;   
;;                 开始循环                                 ;
;###########################################################;
;;       TMS320F2812DSP资源分配列表                         ;
;;DSP资源分配       	主要用途                            ;
;;    AR0	       暂存n－1＋(m-1)*13的索引值               ;
;;    AR1	      用作读写数据空间的索引值                  ;
;;   XAR2	     作数据空间首地址，*+XAR2[AR1]对数据空间读写;
;;    AR3	    作外循环寄存器变量                          ;
;;    AR4	   作内循环寄存器变量                           ;
;;   XAR5	  暂存非标准球谐系数 ，用在循环的累加中        ;
;;   XAR6	 暂存非标准球谐系数 ，用在循环的累加中         ;
;;   XAR7	暂存cos( m λ)                                  ;
;###########################################################;
;###########################################################;
   MOV AR3,#1                    ;n=AR3
   MOV AR4,#0                    ;m=AR4 
   MOVW DP,#ar 
LOOP:
   SPM 0
   MOVL XT,@ar
   IMPYL P,XT,@aor
   QMPYL ACC,XT,@aor
   LSL64 ACC:P,#2
   MOVL @ar,ACC                  ;ar=ar*aor Q30 
LOOP1:   
   MOV T,#13
   MPYU ACC,T,AR4
   ADD AL,AR3
   LSL AL,#1
   MOV AR0,AL         
   SUBB XAR0,#14*2               ;AR0=[n-1+(m-1)*13]*2
   
   ;$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$;
   ;         递归计算p[m][n]和dp[m][n]=x[m][n]              ;
   ;$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$;
   ;if(n==m)  
   SPM 0   
   MOV AL,AR3
   CMP AL,AR4
   SBF L1,NEQ
   MOVL XT,@ct         
   MOVL XAR2,#pp
   IMPYL P,XT,*+XAR2[AR0]
   QMPYL ACC,XT,*+XAR2[AR0] 
   LSL64 ACC:P,#1
   MOV AR1,AR0
   ADDB XAR1,#14*2               ;AR1=[n+m*13]*2
   MOVL *+XAR2[AR1],ACC          ;pp[m][n]=ct*pp[m-1][n-1] Q30
   MOVL XT,@st
   IMPYL P,XT,*+XAR2[AR0]
   QMPYL ACC,XT,*+XAR2[AR0]
   NEG64 ACC:P
   MOVL XAR5,P
   MOVL XAR6,ACC
   MOVL XAR2,#dpp 
   MOVL XT,@ct
   IMPYL P,XT,*+XAR2[AR0]
   QMPYL ACC,XT,*+XAR2[AR0]
   ADDUL P,XAR5
   ADDCL ACC,XAR6
   LSL64 ACC:P,#1
   MOVL *+XAR2[AR1],ACC          ;dpp[m][n]=st*pp[m-1][n-1]+ct*pp[m-1][n-1] Q30 
   BF S50,UNC   
                 
L1:
   ;if(n==1&&m==0)
   CMP AR3,#1
   SBF L2,NEQ
   CMP AR4,#0
   SBF L2,NEQ
   MOVL XAR2,#pp
   MOV AR1,AR0
   ADDB XAR1,#13*2
   MOVL XT,@st
   IMPYL P,XT,*+XAR2[AR1]
   QMPYL ACC,XT,*+XAR2[AR1]
   LSL64 ACC:P,#1
   ADDB XAR1,#1*2
   MOVL *+XAR2[AR1],ACC          ;pp[m][n]=ct*pp[m][n-1] Q30
   SUBB XAR1,#1*2
   MOVL XT,@ct
   IMPYL P,XT,*+XAR2[AR1]
   QMPYL ACC,XT,*+XAR2[AR1]
   MOVL XAR5,P
   MOVL XAR6,ACC
   MOVL XAR2,#dpp
   MOVL XT,@st
   IMPYL P,XT,*+XAR2[AR1]
   QMPYL ACC,XT,*+XAR2[AR1]
   ADDUL P,XAR5
   ADDCL ACC,XAR6
   LSL64 ACC:P,#1
   ADDB XAR1,#1*2
   MOVL *+XAR2[AR1],ACC          ;dpp[m][n]=ct*dpp[m][n-1]-st*pp[m][n-1] Q14 
   BF S50,UNC
                     
L2: ;if(n>1&&n!=m)
   CMP AR3,#1
   B S50,LEQ
   MOV AL,AR3
   CMP AL,AR4
   SBF S50,EQ
   MOV AR2,AR4
   ADDB XAR2,#2
   MOV AL,AR2
   CMP AL,AR3
   B SS50,LEQ     
   MOVL XAR2,#pp                 ;if(m>n-2)
   MOV AR1,AR0
   ADDB XAR1,#12*2
   MOVB ACC,#0
   MOVL *+XAR2[AR1],ACC
   MOVL XAR2,#dpp
   MOVL *+XAR2[AR1],ACC
SS50:
   MOVL XAR2,#k
   MOV AR1,AR0
   ADDB XAR1,#28
   MOVL XT,*+XAR2[AR1]
   MOVL XAR2,#pp
   SUBB XAR1,#4
   IMPYL P,XT,*+XAR2[AR1]
   QMPYL ACC,XT,*+XAR2[AR1]
   NEG64 ACC:P 
   MOVL XAR5,P
   MOVL XAR6,ACC  
   MOVL XT,@st
   ADDB XAR1,#2
   IMPYL P,XT,*+XAR2[AR1]
   QMPYL ACC,XT,*+XAR2[AR1]
   ADDUL P,XAR5
   ADDCL ACC,XAR6
   LSL64 ACC:P,#1 
   ADDB XAR1,#2
   MOVL *+XAR2[AR1],ACC          ;pp[m][n]=st*pp[m][n-1]-k[m][n]*pp[m][n-2],Q30
   MOVL XAR2,#k
   MOVL XT,*+XAR2[AR1]
   MOVL XAR2,#dpp
   SUBB XAR1,#4
   IMPYL P,XT,*+XAR2[AR1]
   QMPYL ACC,XT,*+XAR2[AR1]
   NEG64 ACC:P
   MOVL XAR5,P
   MOVL XAR6,ACC                 ;XAR6:XAR5=-k[m][n]*dp[m][n-2] XAR1 cannot be used
   MOVL XT,@ct                   ;//////////////////////////////
   ADDB XAR1,#2
   MOVL XAR2,#pp
   IMPYL P,XT,*+XAR2[AR1]        ;/////////////////////
   QMPYL ACC,XT,*+XAR2[AR1]
   ADDUL P,XAR5
   ADDCL ACC,XAR6
   MOVL XAR5,P
   MOVL XAR6,ACC                 ;XAR6:XAR5=ct*p[m][n-1]-k[m][n]*dp[m][n-2]
   MOVL XT,@st
   MOVL XAR2,#dpp
   IMPYL P,XT,*+XAR2[AR1]
   QMPYL ACC,XT,*+XAR2[AR1]
   ADDUL P,XAR5
   ADDCL ACC,XAR6
   LSL64 ACC:P,#1
   ADDB XAR1,#2
   MOVL *+XAR2[AR1],ACC          ;dpp[m][n]=st*dpp[m][n-1]+ct*pp[m][n-1]-k[m][n]*dpp[m][n-2]
 
S50: 
   ;$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$;
   ;             及时球谐系数求取                            ;
   ;$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$; 
   MOV AH,AR0
   ASR AH,#1                     ;Warning:using LSR is wrong
   MOV AR1,AH
   ADDB XAR1,#14                 ;AR1=m*13+n
   MOVW DP,#dt
   MOV T,@dt 
   MOVW DP,#ar   
   MOVL XAR2,#cd
   MPY ACC,T,*+XAR2[AR1] 
   MPY P,AR1,#2         
   MOV AR1,P                     ;AR1=[m*13+n]*2
   MOVL XAR2,#c
   ADDL ACC,*+XAR2[AR1] 
   ;MOVL XAR2,#tc                ;*************************
   ;MOVL *+XAR2[AR1],ACC         ;*************************
   MOVL XAR5,ACC                 ;XAR5=tc[m][n]=c[m][n]+dt*cd[m][n] Q15,32位
   CMP AR4,#0
   BF L3,EQ                      ;//////////////////////////////////////
   MPY ACC,AR3,#13
   ADD AL,AR4
   MOV AR1,AL
   DEC AR1
   MOVW DP,#dt
   MOV T,@dt 
   MOVW DP,#ar 
   MOVL XAR2,#cd
   MPY ACC,T,*+XAR2[AR1]
   MOVL XAR2,#c 
   MPY P,AR1,#2
   MOV AR1,P
   ADDL ACC,*+XAR2[AR1]
   ;MOVL XAR2,#tc                ;******************************
   ;MOVL *+XAR2[AR1],ACC         ;******************************
   MOVL XAR6,ACC                 ;XAR6=tc[n][m-1]=c[n][m-1]+dt*cd[n][m-1] Q15,32位
   
L3: 
   ;$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$;
   ;             地心坐标系中地磁要素的计算                 ;
   ;$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$;
   MOVL XAR2,#pp
   MOVL XT,@ar
   MOV AR1,AR0
   ADDB XAR1,#14*2
   IMPYL ACC,XT,*+XAR2[AR1]
   QMPYL ACC,XT,*+XAR2[AR1]
   LSL64 ACC:P,#2
   MOVL @par,ACC                 ;par=ar*pp[m][n] Q30,32位
   
   CMP AR4,#0
   SBF L4,NEQ
   ;MOVL XAR2,#tc                ;*****************************
   ;MOVL ACC,*+XAR2[AR1]         ;*****************************
   MOVL ACC,XAR5
   MOV T,#1
   ASRL ACC,T
   MOVL @temp1,ACC               ;temp1=tc[m][n]*cp[m] Q14,32位
   MOVB ACC,#0
   MOVL @temp2,ACC               ;temp2=tc[m][n]*spp[m] Q14,32位
   BF L5,UNC
   
L4:  
   ;MOVL XAR2,#tc
   ;MOVL XAR5,*+XAR2[AR1]        ;XAR5暂存tc[m][n]
   ;MPY ACC,AR3,#13
   ;ADD AL,AR4
   ;MOV AR1,AL
   ;DEC AR1
   ;MOVL XAR6,*+XAR2[AR1]        ;XAR6暂存tc[n][m-1]
   MOVL XAR2,#cp
   MOV AH,AR4                    ;WARNING: using 2*m
   LSL AH,#1
   MOV AR1,AH
   MOVL XT,*+XAR2[AR1]
   MOVL XAR7,XT                  ;XAR7暂存cp[m]
   QMPYL P,XT,XAR5        
   MOVL XAR2,#spp
   ;MOV AR1,AR4
   MOVL XT,*+XAR2[AR1]
   MOVB ACC,#0
   QMPYAL P,XT,XAR6
   ADDL ACC,P      
   MOVL @temp1,ACC               ;temp1=tc[m][n]*cp[m]+tc[n][m-1]*sp[m] Q30
   QMPYL ACC,XT,XAR5
   MOVL XT,XAR7
   QMPYL P,XT,XAR6
   SUBL ACC,P
   MOVL @temp2,ACC               ;temp2=tc[m][n]*sp[m]-tc[n][m-1]*cp[m] Q14

L5:
   SPM 0
   MOVL XAR2,#dpp 
   MOV AR1,AR0
   ADDB XAR1,#14*2
   MOVL XT,@ar
   QMPYL P,XT,*+XAR2[AR1]
   MOVL XT,P
   IMPYL P,XT,@temp1
   QMPYL ACC,XT,@temp1   
   LSL64 ACC:P,#3                ;bx=bx-ar*temp1*dp[m][n],Q13
   NEG64 ACC:P
   ADDUL P,@bx0
   ADDCL ACC,@bx
   MOVL @bx0,P
   MOVL @bx,ACC 

   MOVL XT,@temp2
   IMPYL P,XT,@par
   QMPYL ACC,XT,@par
   LSL64 ACC:P,#1
   MOVL XAR5,ACC
   MOV AH,AR4
   MOV AL,#0
   LSL ACC,#11    
   MOVL XT,ACC                   ;XT=fm[m]=m Q27         
   IMPYL P,XT,XAR5
   QMPYL ACC,XT,XAR5
   LSL64 ACC:P,#5
   CLRC C  
   ADDUL P,@by0
   ADDCL ACC,@by      
   MOVL @by0,P
   MOVL @by,ACC                  ;by+=fm[m]*temp2*par,Q13
    
   MOVL XT,@temp1
   IMPYL P,XT,@par
   QMPYL ACC,XT,@par
   LSL64 ACC:P,#1
   MOVL XAR5,ACC
   MOV AH,AR3
   INC AH
   MOV AL,#0
   LSL ACC,#11 
   MOVL XT,ACC                   ;XT=fn[n]=n+1 Q27         
   IMPYL P,XT,XAR5
   QMPYL ACC,XT,XAR5
   LSL64 ACC:P,#5
   CLRC C  
   NEG64 ACC:P
   ADDUL P,@bz0
   ADDCL ACC,@bz   
   MOVL @bz0,P
   MOVL @bz,ACC                  ;bz-=fn[n]*temp1*par,Q13   
   
   MOVL XAR2,#dpp 
   MOV AR1,AR0
   ADDB XAR1,#14*2
   MOVL XT,@ar
   QMPYL P,XT,*+XAR2[AR1]
   MOVL XT,P
   IMPYL P,XT,@temp2
   QMPYL ACC,XT,@temp2   
   LSL64 ACC:P,#3                ;if(st==0) byy=byy-ar*temp2*dp[m][n],Q13
   NEG64 ACC:P
   ADDUL P,@byy0
   ADDCL ACC,@byy
   MOVL @byy0,P
   MOVL @byy,ACC  
          
L6:
   INC AR4
   MOV AL,AR4
   CMP AL,AR3
   BF LOOP1,LEQ                  ;内循环判断
   MOV AR4,#0
   INC AR3
   CMP AR3,#MAXORD
   BF LOOP,LEQ                   ;外循环判断                   
;///////////////////////////////////////////////////////////; 
;//////////////////////  循环结束   ////////////////////////;
;///////////////////////////////////////////////////////////;  


;###########################################################;
;             地心坐标参量转化为大地坐标参量                ;
;###########################################################; 
;###########################################################;
   MOVL ACC,@byy
   CMP @ct,#0
   MOVL @by,ACC,EQ               ;if(ct==0) by=byy,Q13
   CMP @ct,#0
   SBF flag,EQ
   
   MOVL P,@ct                    ;P=ct in Q31
   MOVL ACC,@by
   SETC SXM
   SFR ACC,#1
   LC _div32
   MOVL @by,P                    ;by=by/ct,Q13
flag:
   SPM 1
   MOVL XT,@ca
   QMPYL P,XT,@bx
   MOVL XT,@sa
   MOVB ACC,#0
   QMPYAL P,XT,@bz
   ADDL ACC,P<<PM
   MOVL XAR2,ACC                 ;XAR2 store x Q13
   ;ADD  ACC,#1000h              ;四舍五入
   ;SETC SXM                     ;; 注意之处
   ;SFR  ACC,#13                 ;;将数据从Q13转化为Q0
   MOVW DP,#_x
   MOVL @_x,ACC                  ;x=bx*ca+bz*sa Q13
   
   MOVW DP,#by
   MOVL ACC,@by
   MOVL XAR3,ACC                 ;XAR3 store y Q13
   ;ADD  ACC,#1000h              ;四舍五入
   ;SETC SXM                   
   ;SFR  ACC,#13                 ;;将数据从Q13转化为Q0
   MOVW DP,#_y    
   MOVL @_y,ACC                  ;y=by Q13
   
   MOVW DP,#sa
   MOVL XT,@sa
   MOVW DP,#bx
   QMPYL P,XT,@bx
   MOVB ACC,#0
   MOVW DP,#ca
   MOVL XT,@ca
   QMPYSL P,XT,@bz
   ADDL ACC,P<<PM  
   ;MOVL XAR4,ACC                ;XAR4 store z Q13
   ;MOVW DP,#z_q13
   ;MOVL @z_q13,ACC              ;z_q13 store z Q13  计算ti备用
   ;ADD  ACC,#1000h
   ;SETC SXM                     ;; ////////////
   ;SFR  ACC,#13                 ;;将数据从Q13转化为Q0
   MOVW DP,#_z 
   MOVL @_z,ACC                  ;z=bz*ca-bx*sa Q13
   
;###########################################################;
;###########################################################;
;             计算水平磁场h和总磁场ti                       ;
;###########################################################;
;###########################################################;
   SPM 0
   MOVL XT,XAR2
   IMPYL P,XT,XAR2
   QMPYL ACC,XT,XAR2
   MOVL XAR0,P
   MOVL XAR1,ACC
   MOVL XT,XAR3
   IMPYL P,XT,XAR3    
   QMPYL ACC,XT,XAR3
   ADDUL P,XAR0
   ADDCL ACC,XAR1
   LC  _qsqrt
   MOVL @h_q13,ACC               ;h_q13 store h Q13 计算ti备用
   ADD  ACC,#1000h               ;四舍五入
   SETC SXM                  
   SFR  ACC,#13                  ;;将数据从Q13转化为Q0 
   MOVL @_h,ACC                  ;h=sqrt(x*x+y*y) Q0
   
   MOVL XT,@h_q13
   IMPYL P,XT,@h_q13
   QMPYL ACC,XT,@h_q13
   MOVL XAR0,P
   MOVL XAR1,ACC
   MOVW DP,#z_q13
   MOVL XT,@z_q13
   IMPYL P,XT,@z_q13   
   QMPYL ACC,XT,@z_q13
   ADDUL P,XAR0
   ADDCL ACC,XAR1
   LC  _qsqrt
   ADD  ACC,#1000h               ;四舍五入
   SETC SXM                  
   SFR  ACC,#13                  ;;将数据从Q13转化为Q0
   MOVW DP,#_ti
   MOVL @_ti,ACC                 ;ti=sqrt(h*h+z*z) Q0

   CLRC SXM
   CLRC OVM
   NOP
   NOP
   
   ;保护现场
   POP ST1
   POP ST0	
   POP P
   POP XT
   POP XAR7
   POP XAR6
   POP XAR5
   POP XAR4
   POP XAR3
   POP XAR2
   POP XAR1
   POP XAR0
   POP ACC


   LRETR                         ;返回到MagneticCalcu主程序
   .end